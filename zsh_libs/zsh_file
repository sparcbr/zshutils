VERSION=1.0.7

setopt nomultios

function getfilename()
{
	echo "${1:t}"
}

function getfullpath()
{
	local relative
	zparseopts -D - r:=relative
	(( $# )) || return 1
	[[ -n $relative ]] && type realpath >/dev/null &&
		realpath --relative-base=${relative[2]#=} $1 2>/dev/null || echo "${(z)1:P}"
}
function getpath()
{
	echo "${1:h}"
}

# ${file%%.old}
function getext()
{
	# echo "${1##*.}"
	echo "${1:e}"
}

function getfilenoext()
{
	echo "${1:t:r}"
}

function isDir()
{
	local dir mk
	[[ $1 = -m ]] && { mk=1; shift }
	dir=$1
	if [[ ! -d $dir ]]; then
		if [[ -e $dir ]]; then
			techo -c err "$dir is a file."
			return 2
		fi
		[[ -z $mk ]] && return 1
		confirm "dir $dir doesn't exist. Create" && run -S mkdir $dir
	fi
}

function isEmptyDir()
{
	local glob=("$1"(N/F))
	[[ -z $glob ]]
}

# @description: remove extension
# @arg2: optional extension. If given remove only if extension = this
# @returns: full path (or filename if no path given) without extension
function noext()
{
	if [[ -n $2 ]]; then
		echo "${1%%.$2}"
	else
		echo "${1:r}"
	fi
}

# Added 2019-04-15 18:36 @TODO
function replace()
{
	local s r
	s=$1 ; r=$2 ; shift 2
	sed -i bak -e s/$1/$2/g "$@"
}

#@TODO
function backup()
{
	local sudo=""
	[[ "$1" == "-s" ]] && { shift; sudo='-s' }
	[[ -n "$2" ]] && {
		run $sudo cp "$1" "$2/$(getfilename "$1").bkup"
	} || run $sudo cp "$1" "$1.bkup"
}

#@TODO
function rename()
{
	local orig name dir fullpath
	(( $# )) || usage $0
	[[ "$1" == '-s' ]] && { shift; sudo='-s' }
	orig="$(getfilename "$1")"
	dir="$(getpath $(getfullpath $1))"
	fullpath="$dir/$orig"
	[[ -f "$1" || -d "$1" ]] || abort 127 "$1 doesn't exist or no file permission"
	name=${2:-$orig}
	if input -p "Rename \"${C[warn]}$fullpath${C[head]}\" to" -v name && \
		[[ -n $name && $name != $orig ]]
	then
		run $sudo mv "$fullpath" "$dir/$name"
	fi
}

function ddiff()
{
	local list cmd file sudo config var dargs r u q
	zparseopts -D -M - c=config -set=c l=list s=sudo i:=ignore q=q

	echo "dir1=$dir1\ndir2=$dir2"
	[[ -n $list ]] && return 0
	
	dargs=(-u -r)
	[[ -n $q ]] && dargs+=($q)

	if [[ -n $config || -z $dir1 || -z $dir2 ]]; then
		if [[ -n $config ]]; then
			if [[ -n $1 ]]; then
				isDir $1 && dir1=${1%/} || return
				shift
			fi
			if [[ -n $1 ]]; then
				isDir $1 && dir2=${1%/} || return
				shift
			fi
		fi
		if [[ -z $dir1 ]]; then
			input -p "Dir 1" -v var || return
			isDir -m $var && dir1=$var || return
		fi
		if [[ -z $dir2 ]]; then
			input -p "Dir 2" -v var || return
			isDir -m $var && dir2=$var || return
		fi
		d1=$dir1; d2=$dir2
		echo "dir1=$dir1\ndir2=$dir2"
	fi

	if [[ -d $dir1 && -d $dir2 ]]; then 
		if [[ -n $1 ]] && type $1 >/dev/null; then
			cmd=$1
			shift
		fi
		if [[ -z $cmd ]]; then
			[[ -n $1 ]] && run $sudo -p "diff -ur \$d1/$1 \$d2/$1" diff $dargs $dir1/$1 $dir2/$1 ||
				run $sudo -p "diff -ur \$dir1 \$dir2" diff $dargs $dir1 $dir2
		else
			[[ -n $1 ]] && run $sudo -p "${=cmd} \$dir1/$1 \$dir2/$1" $=cmd $dir1/$1 $dir2/$1 ||
				run $sudo -p "${=cmd} \$dir1 \$dir2" $=cmd $dir1 $dir2
		fi
	fi
}

# Added 2019-05-03 16:50
function syncDirs()
{
	local integer i n
	local src targets dir data rsArgs checksum arg
	local quiet sudo ignores dry extraArgs reverse
	local files stat name
	typeset -a src targets rsArgs extraArgs files
	zparseopts -D -M - q=quiet s=sudo i:=ignores d=dry t:=targets c=checksum r=reverse

	[[ -z "$sudo" ]] && sudo='-S'
	[[ $# -ge 2 ]] || abort 1 "usage: $0 source1 [.. sourceN] target1[,target2]"
	
	n=$#
	for arg; do
		if [[ ${arg:0:1} = - ]]; then # option parameter
			extraArgs+=($arg)
		else # sources
			src+=("$arg[@]")
		fi
	done
	if [[ $#src -ge 2 ]]; then
		explode -v targets $src[-1] ','
		src[$#src]=()
	fi
	#if [[ $#src -eq 0 ]]; then
	#	src=($(box get backupconfig))
	#fi
	if [[ -z "$targets" ]]; then
		input "Enter target directory" -v targets || cancel
	fi

	if [[ -n $reverse ]]; then
		files=($targets)
		targets=($src)
		src=($files)
	fi


	# # # # # # # # # #	   -t, --times           preserve modification times
	# rsync arguments #    -O, --omit-dir-times  omit directories from --times
	# # # # # # # # # #    -J, --omit-link-times
	rsArgs=( -hlDr )
	if [[ -n $checksum ]]; then
		rsArgs+=(-c --no-times)
	else
		rsArgs+=(-tu)
	fi 
	# exclusions
	rsArgs+=(--exclude=.osync_workdir --exclude='backup*' --exclude='*.local' --exclude='.bsync*')
	[[ -n $ignores ]] && rsArgs+=(--exclude-from="${ignores[2]}")
	for ((n=1; n<=$#src; n++)); do
		if [[ -d "${src[$n]}" ]]; then
			if [[ -f "${src[$n]}/rsync.ignore" ]]; then
				rsArgs+=(--exclude-from="${src[$n]}/rsync.ignore")
			fi
			if isEmptyDir $src[$n]; then
				techo -c err "Empty directory$C_: $C[lred]$src[$n]"
				return 1
			fi
			[[ ${src[$n]: -1} != / ]] && src[$n]+=/
		fi
	done
	
	techo -c lcyan "Sources: ${(@qq)src}"
	techo -c lcyan "Targets: ${(@qq)targets}"
	# rsync arguments from command line
	rsArgs+=($extraArgs)
	techo -c lcyan "rsync args: ${(@qq)rsArgs}"

	# # # # # #
	# Targets #
	# # # # # #
	local nothing=1 permFlags
	for dir in $targets; do
		if isDir -m "$dir"; then
			[[ "$(fstypeof $dir)" != "vfat" ]] && permFlags='-pog'
			data="$(run $sudo rsync -ni ${rsArgs[@]} $permFlags "$src[@]" $dir)"
			if [[ $#data -gt 0 ]]; then
				nothing=0
				echo $data
				confirm "Do it" && # run --confirm "Do it"
					run $sudo rsync --info=PROGRESS2 $rsArgs $permFlags $src $dir || abort
				_sync
			fi

			# # # # # # #
			# Deletions #
			# # # # # # #
			data="$(run $sudo rsync -ni --delete --existing --ignore-existing \
				-JO ${rsArgs[@]} $src[@] $dir)" || abort

			if [[ -n "$data" ]]; then
				nothing=0
				techo -c warn "Deletions:"
				n=0
				echo $data | while read stat name; do
					echo "$dir/$name ${C[red]}âŒ$C_"
					files+=($dir/$name)
					((n++))
				done
				confirm -c warn "Delete these $n files" || continue
				#@TODO rm $(chooser "$files[@]")
				run $sudo rsync --delete --existing --ignore-existing \
					--info=PROGRESS2 $rsArgs $src $dir || abort
				_sync
			fi
		fi
	done
	[[ $nothing -eq 1 ]] && techo -c cyan "Nothing to do"
	return 0
}

# Added 2019-01-22 18:07:09 @TODO
function syncIhm()
{
	( cd $HOME/PeD/android && {
		syncDirs -c -i $CFG/android.ignore $TREADMILL \
			$CLOUD_APPS/Treadmill,/m/home/inbramed/android/Treadmill
		syncDirs -c -i $CFG/android.ignore $CICLOERGOMETER \
			$CLOUD_APPS/Cicloergometer,/m/home/inbramed/android/Cicloergometer
	  }
	)
}

# Added 2019-02-20 09:23:39 @TODO
function box()
{
	local box base=$HOME/repos search=$1
	#zparseopts -D -M - S:=search
	#include boxes
	#box start
	if [[ -n $search && -d $base/$search ]]; then
		box=$search
	else
		local list=($base/*(-/om))
		box=$(chooser ${=1:+-S $1} $list:t) || cancel
		[[ -d $base/$box ]] || abort 127
	fi
	cd $base/$box
	todo -d . #@TODO mv to cd hook
}


# Added 2019-06-05 16:55
function attributeSync()
{
	local stats name dir1 dir2 rsArgs skip dry
	#[[ $# -gt 2 ]] || return 10
	zparseopts -D - d=dry

	[[ -n $dry ]] && dry=techo
	[[ -n $1 ]] && { dir1=$1 ; shift }
	[[ -n $1 ]] && { dir2=$1 ; shift }

	if [[ ! -d $dir1 || ! -d $dir2 ]]; then
		[[ -d $dir1 ]] || techo -c lred "$dir1: not a directory"
		[[ -d $dir2 ]] || techo -c lred "$dir2: not a directory"
		return 127
	fi
	confirm "$dir1 -> $dir2" || return
	rsArgs=(${@:--u})
	while read stats name skip; do
		#echo stat=$stats name=$name ;
		[[ $stats == skipping ]] 
		[[ $stats[5] == t ]] && $dry sudo touch --reference "$dir1/$name" $dir2/$name
		[[ $stats[6] == p ]] && $dry sudo chmod --reference "$dir1/$name" $dir2/$name
		if [[ $stats[7] == o || $stats[8] == g ]] && [[ $stats != skipping ]]; then
			$dry sudo chown --reference "$dir1/$name" $dir2/$name
		fi
	done < <(run -s rsync -n -hDirtpog $rsArgs $dir1/ $dir2 | tee $TTY)
	_sync
}

function _sync() {
	run -w "isDirty" -p "Flushing caches to disk. Please wait." sync
	
	#watch -n 1 -c -t  "grep -e 'Dirty:' -e 'Writeback:' /proc/meminfo; kill -0 $pid || killall watch"
}

# Added 2019-08-21 00:27
function fordir() #TODO
{
	local d cmd args dirs i=1 verbose
	zparseopts -D -M - v:=verbose
	[[ $# -gt 0 ]] || usage
	if ((! $+verbose[2])); then
		verbose=(-v 0)
	fi
	for d; do
		if [[ -d "$d" ]]; then
			dirs+=("$d")
		elif type "$d" 2>&1 >/dev/null; then
			cmd="$d"
			shift
			break
		fi
		shift
	done
	[[ -n $cmd ]] || usage 'Missing command'
	args=("$@")
	[[ -n "$dirs" ]] || dirs=(*(-/))
	for d in $dirs; do
		run $verbose "$cmd" $args "$d"
	done
}

# Added 2019-06-07 17:27
function fordo() #TODO
{
	e FIXME
	local v cmd
	cmd=$1; shift
	for v; do
		run "$cmd" "$v"
	done
}

function show() #TODO
{
	local cmd
	type bat 2>&1 >/dev/null && cmd='bat' || cmd='cat'
	$cmd "$@"
}
