VERSION=1.0.7
[[ -v comp ]] || typeset -g -a comp
alias e=echo
alias op='open'
alias vmake="make SHELL='$SHELL -x'"
alias sv="v -s"
alias cfg="cd $CFG"
alias s="do_sudo"
alias apt="sudo apt"
alias sys="sudo systemctl"
alias c='cod'
alias wh='what'
alias pizza='alarm 13 pizza time'

gExcludeDirs=(__tests__ .local)
gNodeExcludeDirs=(node_modules)
gRNExcludeDirs=(android android/app/build ios $gNodeExcludeDirs)
gPhPExcludeDirs=(vendor)

function cod()
{
	code --goto "$@"
}

comp+=("what=which")
function what()
{
	builtin which $1
}

function hg()
{
	local match fopt gopt ic range
	zparseopts -D - i=ic r:=range
	#gopt+=($ic ${@#-[]})
	gopt+=($ic)
	[[ -n $range ]] && fopt=${range[2]} || fopt="0"
	#@TODO
	fc -li ${=fopt} | egrep ${=gopt} -- $1
}

function aptlist() # Added 2019-04-12 11:45
{
	zgrep 'install ' /var/log/dpkg.log* | sort | cut -f1,2,4,6 -d' ' | less
}

function zindent()
{
	integer f
	(($#>1)) && {
		f=1
		echo "function $1()\n{"
		shift
	}
	eval "function temp() { "$@" }" || return 1
	echo $functions[temp] # @TODO: comments are lost, find a way to re-add them
	((f)) && echo "}"
	return 0
}

function mkscript()
{
	local shell func name edit append ret=1
	local file isLib help line data data2 _type
	zparseopts -D -K s:=shell f:=func n:=name e=edit

	(($#func)) && func=$func[2]
	data2="$*"
	while ((ret)); do
		input -p "Function name" -v func || cancel
		if (($#func)); then
			_type=($(typeOf -a $func))
			if in_array 'function' _type; then
				local tmp=$functions_source[$func] options
				options=(Rename Append "Edit $func()" Cancel)
				if [[ -z $tmp ]]; then
					if explode -v tmp $functions[$func] $'\n' 1 && \
						[[ $tmp == *'include '(#b)([a-zA-Z]##)" && {" ]] # lazyload
					then
						tmp=zsh_$match[1]
					else
						tmp='anonymous'
						options=(Rename 'Use it' Cancel)
					fi
				fi
				# @TODO
				#if [[ $arg[4] == 'autoload' ]]; then
				#	autoload +X $1 && edfunc $1
				#	return

				chooser -v tmp -f1 -p "$func() already exists ($tmp)" $options || cancel

				case $tmp in
					Edit)
						vw $func && return
						ret=0
						;;
					Append)
						ret=0
						append=1
						;;
					Cancel)
						cancel
						;;
					Rename)
						;;
					Use)
						# @TODO lookup comments (if not annonymous)
						data2="$functions[$func]"
						ret=0
						;;
					*) abort -D-1 9 "Unknown option '$tmp'" ;;
				esac
			elif in_array 'command' _type; then
				techo -c warn "Command $func exists"
			else
				ret=0
			fi
		fi
	done

	# Choose zsh lib
	isLib=0
	if [[ -n "$name" ]]; then
		local err_exists="exists but isn't a shell script."
		name=$name[2]
        if [[ $(getpath $name) != '.' || $name == ./* ]]; then # full path given
			if [[ -d $name ]]; then
				echo "$name is a directory"
				return 1
			elif [[ -f $name ]] && ! isScript $name; then
				echo "$name $err_exists"
				return 1
			fi
			file=$name
		else
			cmdtype=($(typeOf $name))
			type=${cmdtype[1]}
			file=${cmdtype[2]}
			name=${name#zsh_*}
			if [[ $type != "script" ]]; then
				if [[ -f "$ZSH_LIBS/zsh_$name" ]]; then # is it a lib?
					file="$ZSH_LIBS/zsh_$name"
					isLib=1
				elif [[ "$type" != 'none' ]]; then
					echo "$file $err_exists"
					return 1
				else
					file="$ZSH_LIBS/zsh_$name"
					isLib=1
				fi
			fi
		fi
	else
		isLib=1
		chooser -v file $ZSH_LIBS/{zsh_[a-z]##,zsh_*.*}(-N) "New zsh library" "Browse full path" || cancel
		if [[ $file == "New zsh library" ]]; then
			input -v file -p "Enter new library name" 'zsh_' || cancel
		elif [[ $file == "Browse full path" ]]; then
			#@TODO
			chooser -v file --file --head "Choose path of zsh library" || cancel
		fi

		name=$(getfilename -p 'zsh_' $file)
		if [[ $file == */* ]]; then
			file=$(getpath $file)/$name
		else
			file=$ZSH_LIBS/$name
		fi
	fi

	if [[ ! -f "$file" ]]; then
		# Create library
		if ((isLib)); then
			data="$(< $ZSH_LIBS/zsh_blank)"
		else
			# Create script
			[[ -n $shell ]] && shell="/bin/"$shell[2] || shell=$SHELL
			data="#!${shell}"
		fi
	fi

	# add data
	# TODO: append to function in case of existing function
	# ((apend)) zindent $data2
	data+="\n# Added "$(date "+%F %R")"\n"
    if [[ -n $func ]]; then
		data+="$(zindent $func "$data2")" ||
	else
		line=$(wc -l $file)
		data+="$(zindent "$data2")"
	fi

	echo -n "$data" >> $file
	((isLib)) && source $file && { type $func ; what $func; }

	# setup file
	[[ -x $file || $isLib -eq 1 ]] || chmod +x $file

	# edit
	if [[ -n $edit || -z $data2 ]] || { sleep 1; confirm "Edit file" }; then
		if (($#func && isLib)); then
			edfunc $func
		else
			v -f $file
		fi
	fi
}
#function _stack()
#{
#	declare -A stack
#}
# args: name commandline options

function gvimdiff()
{
	local gopts
	integer ret
	zparseopts -D -M - f=gopts
	diff -u $1 $2
	ret=$?
	if ((ret==0)); then
		techo "$1 == $2"
	elif ((ret == 1)); then
		command gvimdiff $gopts $1 $2
	fi
}

function emu()
{
    v /media/sparc/retropie/opt/retropie/emulators/retroarch/retroarch.cfg
    xdg-open https://github.com/retropie/retropie-setup/wiki/retroarch-configuration
    xdg-open http://www.instructables.com/id/USB-SNES-Controller/
}
function add_key()
{
	base=${1:-~}
	cat $CFG/id_rsa.pub > ${base}/.ssh/authorized_keys
	chmod 700 ${base}/.ssh
	chmod 600 ${base}/.ssh/authorized_keys
}

function edalias()
{
	local linenum pat file var isFunc
	integer col
	zparseopts -D -M l:=linenum f=isFunc
	[[ -n $1 ]] || return 1
	if (($#isFunc)); then
		pat="(function +)?$1\(\)"
		linenum=${linenum[2]:-2}
		[[ $linenum != <-> ]] && {
			local array=($(explode "$linenum" ':'))
			linenum=$array[1]
			col=$array[2]
		}
	else
		pat="alias +$1=['\"].*['\"]"
		linenum=0
	fi
	if (($#2)); then
		file=$2
		#var=("${(@f)$(egrep -rHn "^([^#]* )?$pat" $file)}") || var=($file:0)
		if ! var=("${(@f)$(egrep -rHn "^([^#]* )?$pat" $file)}"); then
			[[ $(getfilename $file) == $1 ]] && var=($file:0)
		fi
	else
		var=("${(@f)$(egrep -rHn --exclude tags "^([^#]* )?$pat" $ZSH_LIBS $fpath)}")
	fi

	if [[ -n $var[1] ]]; then
		local result line opts i=1
		typeset -a tmp opts

		if (($#var > 1)); then
			for result in $var; do
				explode -v tmp "$result" ':'
				file=$(getfullpath -r . $tmp[1])
				line=${tmp:2}
				opts+=("$i $file $line")
				((i++))
			done
			i=$(chooser -f1 $opts) || cancel
		fi
		result=$var[$i]
		explode -v tmp "$result" ':'
		file=$tmp[1]
		if ((tmp[2])); then
			[[ ${tmp:2} =~ $pat ]] && {
				((MBEGIN > 1)) && col=$MBEGIN
				# @TODO
				#echo $MATCH $match "{$MBEGIN $MEND} {$mbegin[1] $mend[1]}"
			}
			((linenum+=$tmp[2]))
			file+=:$linenum
			((col)) && file+=:$col
			v $file
		else
			v $file
		fi
	else
		if (($#isFunc)); then
			techo -c err "Could not find function $1()"
		else
			techo -c err "Could not find alias $1"
		fi
	fi
}

function edfunc()
{
	local arg file
	arg=($(type $1)) || return 1
	if [[ "${arg[4]}" == 'autoload' ]]; then
		autoload +X $1 && edfunc $1 $2
		return
	elif [[ $arg[5] != 'function' ]]; then
		if [[ $arg[4] == 'alias' ]] && (($#functions[$1])); then
			file=$functions_source[$1]
		else
			abort 2 "$1 is not a function:\n\t$arg"
		fi
	elif [[ -f $arg[-1] ]]; then
		file=$arg[-1]
	else
		techo "$1 is anonymous function:"
		local tmp=$(what $1)
		techo -c warn $tmp
		if [[ $tmp =~ 'include ([-a-z_]+)' ]]; then
			file=$match[1]
			if [[ $file = */* ]]; then
				file=$(getfullpath $file)
			else
				file="$ZSH_LIBS/zsh_"${file#zsh_}
			fi
		fi
	fi
	edalias ${2:+-l$2} -f "$1" "$file"
}

comp+=("vw=which")
function vw()
{
	local array mime cpath name position
	array=($(explode "$1" ':')) ; name=$array[1]; position=${(j.:.)array[2,-1]}
	array=($(type -w "$name"))
	case "${array[2]}" in
		none)
			[[ -f $name ]] || abort 127 'Command not found'
			v $name:$position
			;;
		function)
			edfunc $name $position
			;;
		alias)
			local func
			if (( $+functions[$name] )); then # also a function exists
				local opt
				(( $#position )) && { edfunc $name $position ; return }
				func=$name
			else
				if [[ $(type $name) =~ "^$name is an alias for (.*)$" ]]; then
					#func=$(inAlias -f $name)
					func=$(inAlias -f $name)
				fi
			fi

			if (($#func)); then
				chooser -v opt -f1 "alias $name" "function $func()" || cancel
				[[ $opt == 'function' ]] && { edfunc $func $position ; return }
			fi
			edalias ${position:+:-l$position} $name
			;;
		command)
			cpath=$(what "$name")
			if [[ -f $cpath ]]; then
				mime=$(getmime $cpath)
				if [[ $mime == 'application/x-sharedlib' ]]; then
					echo "$cpath is binary executable"
					return 1
				fi
				v $cpath${position:+:$position}
			else
				echo Invalid path \"$cpath\"
			fi
			;;
	esac
}

# call vim to each file:linenum argument
# file1:l4 file2:l66
function v()
{
	local opts=() sudo arg args vim
	#typeset -aU
	if [[ -n $DISPLAY ]]; then
		vim=gvim
	else
		vim=vim
	fi
	while (($#)); do
		if [[ ${1:0:1} == '-' ]]; then
			case $1 in
				-h) techo -h -s -f -dis-all -dis-plug -dis-conf
					;;
				-s) sudo=(sudo)
					;;
				-#) opts+=('+/^\s*[^#]')
					;;
				-f) [[ -n $DISPLAY ]] && opts+=(-f)
					;;
				--dis*-conf) opts+=(-u NONE -N -i NONE)
					;;
				--dis*-all) opts+=(-u NONE -N -i NONE --noplugin)
					;;
				--dis*-plug*) opts+=(-N --noplugin -i NONE)
					;;
				*) opts+=($1)
					;;
			esac
		else
			args+=($1)
		fi
		shift
	done

	for arg in $args; do
		if [[ "$arg" =~ ":" ]]; then
			local tmp pos file
			integer i j linenum col
			explode -v tmp $arg ':'
			i=$#tmp
			while [[ $tmp[i] == <-> && $i -gt 1 ]]; do ((i--)); done
			if ((i < $#tmp)); then
				((i++))
				(($#tmp - i)) && col=$tmp[i+1]
				linenum=$tmp[i]
				if ((col)); then
					pos="+call cursor($linenum,$tmp[3])"
				elif ((linenum)); then
					pos="+$linenum"
				fi
				file=$tmp[1,i-1]
				$sudo $vim $opts $pos $file
				continue
			fi
		fi
		$sudo $vim $opts $arg
	done
}

function cptree()
{
	local dest=$1
	shift
	echo $@ | cpio -pdv $dest
}
function loadvim()
{
	set -x
	list=${1:-vimlist.txt}
	dirs=(. $CFG)
	v -p $(cat `find ${dirs[*]} -name $list`)
	set +x

}
function savevim()
{
	list=$(forvim "expand('%:p')")
	echo $list
	echo $list > $CFG/vimlist.txt
}
function vimcmd()
{
	--remote-send $@
}
function forvim()
{
	local _v _cmd
	if [ "$1" = "cmd" ]; then
		_cmd="--remote-send"
		shift
	else
		_cmd="--remote-expr"
	fi
	for _v in `vim --serverlist`; do
		vim --servername $_v $_cmd $@
	done
}
function f()
{
	local base maxDepth args name s opts ex_dirs n=1 fopts=()
	zparseopts -D -M -A opts - d: e: 'i' 's' 'p' g: b:=base
	(($+opts[-d])) && fopts+=(-maxdepth $opts[-d])
	[[ -n "$base" ]] && explode -v base "$base[2]" ',' || base=(.)
	#[[ $1 = - ]] && shift
	#$s find ${=d} \( -path ./bckup_extensions -or -path ./extensions \) -prune -or $name $@ -print
	local found arg
	until (( found )) || [[ -z ${arg::=${@:$n:1}} ]]; do
		[[ $arg[1] == '-' ]] && ((found=n))
		((n++))
	done

	if ((found==0 || found>1)); then
		((found==0)) && args=("$@") || args=(${@:1:$found-1})
		(($+opts[-i])) && name="-iname" || name="-name"
		name=($name' '${^args})
		shift $#args
	fi
	#  #  #  #  #
	# exclusion #
	#  #  #  #  # @TODO read box cfg
	#[[ -n $BOX ]] && projRoot=$HOME/repos/$BOX ||
	ex_dirs=()
	if (($+opts[-e])); then
		IFS=',;: ' read -rA ex_dirs <<<$opts[-e]
	fi
	# EXCLUDE DIRS
	# hasNode node_modules
	# hasAndroid android/app/build
	local prefix='./'
	# if [[ ${base:0:1} != '.' ]]; then
	# fi
	if [[ -f $root/App.js ]]; then
		ex_dirs+=($gRNExcludeDirs)
	elif [[ -f 'package.json' ]]; then
		ex_dirs+=($gNodeExcludeDirs)
	fi
	[[ -d vendor ]] && ex_dirs+=($gPhPExcludeDirs)
	ex_dirs+=($gExcludeDirs)
	chooser -p 'Exclude dirs' -v ex_dirs --multi --selected "$ex_dirs" $ex_dirs
	integer cod=$?
	if ((cod && cod!=10)); then
		confirm 'Cancel' && cancel
	fi
	if (($#ex_dirs)); then
		tmp=('-path '$prefix${(@)^ex_dirs})
		tmp=($(implode tmp ' -o '))
		fopts+=(-type d '(' $=tmp ')' -prune -false -o)
	fi
	[[ -n $g ]] && {
		run ${opts[-s]+-s} find -L $base $fopts $=name "$@" $p | group $g
	} || run ${opts[-s]+-s} find -L $base $fopts $=name "$@" $p
}

function aldd() {
	local R=$USER_HOME/clp
	if [[ $# -gt 1 ]]; then
		if [ "$1" = "-r" ]; then
			shift
			R=$1
			shift
		elif [ "$1" = "-s" ]; then
			R=$STAGING_DIR
			shift
		fi
	fi
	${TOOLCHAIN_SYS}ldd --root=$R $@
}

function recv()
{
	local host=$1
#	scp $host:$2
	echo TODO
}

function send()
{
	local host=$1
	shift
	scp $@ $host
}

function ge()
{
	local sudo icase in_exts ex_exts width prefix root
	local opts args defExDirs defExExts _opts
	integer cod
	typeset -aU ex_dirs ex_files
	zparseopts -D -E -M -A _opts - i=icase s=sudo e: -exclude:=e E: W::=width -cols::=W

	(($#)) || return 10
	opts=()

	root=$(git root)
	#  #  #  #  #
	# inclusion #
	#  #  #  #  #
	IFS=',;: ' read -rA in_exts <<<"$1"; shift
	opts+=('--include=*.'${^in_exts[@]})

	#  #  #  #  #
	# exclusion #
	#  #  #  #  #
	if (($+_opts[-e])); then
		IFS=',;: ' read -rA ex_exts <<<$_opts[-e]
		opts+=('--exclude=*.'${^ex_exts[@]})
	fi

	if (($+_opts[-E])); then
		IFS=',;: ' read -rA ex_dirs <<<$_opts[-E]
	fi
	# TODO read box cfg
	# EXCLUDE DIRS
	if [[ -f 'App.js' ]]; then
		ex_dirs+=($gRNExcludeDirs)
	elif [[ -f 'package.json' ]]; then
		ex_dirs+=($gNodeExcludeDirs)
	fi
	[[ -d vendor ]] && ex_dirs+=($gPhPExcludeDirs)
	ex_dirs+=($gExcludeDirs)

	# EXCLUDE FILE PATTERN
	ex_files=(dev '*.dev*.*' .local local)
	techo "Searching in ${BOX:-$root}/$(getfullpath -r $root $PWD)"
	chooser -p 'Exclude dirs' -v ex_dirs --multi \
		--selected "$ex_dirs $ex_files" -n "$(($#ex_dirs+1))" \
		$ex_dirs "¬¨${C[head]}Exclude\ files:$C_" $ex_files; cod=$?
	if ((cod && cod!=10)); then
		confirm 'Cancel' && cancel
	fi
	(($#ex_dirs)) && opts+=('--exclude-dir='${^ex_dirs[@]})

	# other options
	opts+=(--color=always $icase)
	if [[ -n $sudo ]]; then
		prefix='sudo'
	else
		prefix='command'
	fi
	if (($#width)); then
		# -wN
		(($#width[1] > 2)) && width=${width[1]:2} || width=0
		if ((!width)); then
			$prefix grep -rn $opts "$@"
			return
		fi
	else
		((width=$(tput cols)*3))
	fi
	$prefix grep -rn $opts "$@" | cut -c1-$width

	if (($pipestatus[1] && $#ex_dirs)); then
		if confirm 'Retry without exclusions?'; then
			opts=('--include=*.'${^in_exts[@]} --color=always $icase)
			$prefix grep -rn $opts "$@" | cut -c1-$width
		fi
	fi
}

function duh()
{
	local sudo
	[[ "$1" == '-s' ]] && { shift; sudo='sudo'; }
	$sudo du -csh "$@" | sort -h
}

function stripcolors() # Added 2019-01-11 16:33:46
{
	local sreg="s/\x1B\[([0-9]{1,2}(;[0-9]{1,2})?)?[m|K]//g"
	if (($#)); then
		if [[ -f $1 ]]; then
			sed -r $sreg $1
		else
			echo $@ | sed -r $sreg
		fi
	else
		sed -r $sreg -
	fi
}

function shpydf() # Added 2019-01-11 17:00:37
{
    output=`pydf`; devs=`echo $output | cut -f1 -d' ' `; labels=`sudo lsblk --noheadings -o LABEL $devs|stripcolors`; for line in $output; do i=1; echo "${labels[$i]} $line"; i++; done
}

function find_dupes() # Added 2019-02-25 08:36:38
{
	local dupes
	echo TODO: test against fdupes speed
#	dupes=(find . ! -empty -type f -exec md5sum {} + | sort -k1 | uniq -w32 -D --all-repeated=separate)
	#getpath $(printf "%s\n" "$string1" "$string2" | sed -e 'N;s/^\(.*\).*\n\1.*$/\1/'
}

function lastarg() echo ${@: -1} # Added 2019-03-30 20:31

function logs() # Added 2019-04-11 01:43
{
	[[ $1 == 'off' ]] && { killall tail; return }
	run tail $(git root)/logs/{api,debug,php}.txt -f &
}
function ut() { dt ${1:-now} }
function dt() {
	local date='now' fmt
	if [[ $1[1] == '+' ]]; then
		fmt=$1
	else
		if (($#>1)); then
			fmt=+${2#+}
		fi
		if (($#)); then
			if [[ $1 =~ [^0-9] ]]; then
				date=$1
				(($#fmt)) || fmt='+%s'
			else
				date=@$1
			fi
		fi
	fi
	date --date=$date --utc $fmt
}

function fixtty() # Added 2020-01-14 21:06
{
	# show cursor
	tput cnorm # or echo -en "\e[?25h"
	confirm 'Reset terminal' && tput reset
}
up() { xdotool key Ctrl+Shift+F sleep 0.4 type ‚ùØ ; xdotool key Shift+F3 }
zle -N myComp myComp
myComp() {
	local cmd _type
	cmd=${${(z)BUFFER}[1]}
	_type=$(typeOf $cmd)
	if [[ $_type=='alias' ]]; then
		local tmp=($(inAlias $cmd)) || return
		cmd=$tmp[1]
		_type=$tmp[2]
	fi
	[[ $_type!='function' ]] && return
	what $cmd | grep zparseopts -A 3
}

#bindkey '^X@sr' redo
zle -N up_prompt up
bindkey '^[[1;3A' up_prompt  # alt-up
bindkey ' ' myComp
#bindkey '^[OP' brightness # F1
bindkey '^[r' redo

copy-to-xclip() {
	((REGION_ACTIVE)) && zle copy-region-as-kill
    print -rn -- $CUTBUFFER | xclip -selection clipboard -i
}
zle -N copy-to-xclip
bindkey "^Xw" copy-to-xclip
paste-xclip() {
    killring=("$CUTBUFFER" "${(@)killring[1,-2]}")
    CUTBUFFER=$(xclip -selection clipboard -o)
    zle yank
}
zle -N paste-xclip
bindkey "^Xy" paste-xclip
